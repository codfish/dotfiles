#!/usr/bin/env zsh

export DOTFILES="${HOME}/.dotfiles"
DOTFILES_REPO=${DOTFILES_REPO:-codfish/dotfiles}
DOTFILES_TARBALL_PATH="https://github.com/codfish/dotfiles/tarball/main"
ZSH="$HOME/.dotfiles/oh-my-zsh"
ZSH_CUSTOM="$HOME/.dotfiles/oh-my-zsh-custom"
BREW_PREFIX=$(brew --prefix)

cd ${DOTFILES}

source ./lib/brew
source ./lib/composer
source ./lib/help
source ./lib/list_apps
source ./lib/node
source ./lib/pip
source ./lib/utils
source ./lib/zsh

# Help text
if [[ "$1" == "-h" || "$1" == "--help" ]]; then
  run_help
  exit
fi

# Test for known flags
for opt in $@
do
  case $opt in
    --no-packages) no_packages=true ;;
    --no-sync) no_sync=true ;;
    --configure-macos) configure_macos=true ;;
    --install-casks) install_casks=true ;;
    -*|--*) e_warning "Warning: invalid option $opt" ;;
  esac
done

link() {
  # Force create/replace the symlink.
  ln -fs "${DOTFILES}/${1}" "${HOME}/${2}"
}

mirrorfiles() {
  # Copy `.gitconfig`.
  # Any global git commands in `~/.zshrc.before` will be written to
  # `.gitconfig`. This prevents them being committed to the repository.
  rsync -avz --quiet ${DOTFILES}/dotfiles/gitconfig  ${HOME}/.gitconfig

  # Create the necessary symbolic links between the `.dotfiles` and `HOME`
  # directory.
  link "dotfiles/zshrc"           ".zshrc"
  link "dotfiles/gemrc"           ".gemrc"
  link "dotfiles/curlrc"          ".curlrc"
  link "dotfiles/gitattributes"   ".gitattributes"
  link "dotfiles/gitignore"       ".gitignore"
  link "dotfiles/ripgreprc"       ".ripgreprc"

  e_success "Dotfiles update complete!"
}

# Conditionally sync with the remote repository
if [[ $no_sync ]]; then
  printf "Skipped dotfiles sync.\n"
else
  # Ask before potentially overwriting files
  seek_confirmation "Warning: This step may overwrite your existing dotfiles."

  if is_confirmed; then
    e_header "Syncing dotfiles..."
    # Pull down the latest changes
    git pull --rebase origin main
    mirrorfiles
  else
    printf "Aborting...\n"
  fi
fi

# Install and update packages
if [[ $no_packages ]]; then
  printf "Skipped package installations.\n"
else
  printf "Updating packages...\n"

  # Install & configure zsh plugins
  install_zsh_plugins

  # Install composer
  install_composer

  # Install Homebrew formulae
  run_brew

  # Reset the shell to ensure fnm is available
  source ~/.zshrc

  # Install latest node version
  install_latest_node

  # Install common npm packages
  install_npm_packages

  # Install Python CLI applications via pipx
  install_pipx_packages
fi

# Ask before potentially overwriting macOS defaults
if [[ $configure_macos ]]; then
  seek_confirmation "Warning: This step may modify your macOS system defaults."

  if is_confirmed; then
    bash ./bin/macosdefaults
    e_success "macOS settings updated! You may need to restart."
  else
    printf "Skipped macOS settings update.\n"
  fi
else
  printf "Skipped macOS settings update.\n"
fi

# Conditionally install macOS applications using brew cask
if [[ $install_casks ]]; then
  # Ask before installing apps
  seek_confirmation "Warning: This step will attempt to install macOS GUI applications."

  if is_confirmed; then
    # Install applications through Homebrew Cask
    run_brew_cask
    run_list_apps
  else
    printf "Skipped macOS app installations.\n"
  fi
else
  printf "Skipped macOS app installations.\n"
fi
